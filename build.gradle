buildscript {
	ext {
		springBootVersion = '1.5.1.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "gradle.plugin.org.hidetake:gradle-swagger-generator-plugin:2.4.0"

	}
}
ext {
	apiSpec = 'src/main/resources/api.yaml'
	swaggerConfig = 'src/main/resources/swagger-config.json'
	generatedCode = 'src/swagger-codegen/'
}



apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.hidetake.swagger.generator'

jar {
	baseName = 'airportapiserver'
	version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
    jcenter()
}


sourceSets {
    main {
        java {
            srcDir "${generatedCode}"+'/src/main/java'
        }
    }
}

generateSwaggerCode {
	language = 'spring'
	inputFile = file("$apiSpec")
	outputDir = file("$generatedCode")
	configFile = file("$swaggerConfig")
	components = ['models', 'apis']
}

generateSwaggerUI {
	inputFile =  file("$apiSpec")
	outputDir = file('src/main/resources/static')
	options.docExpansion = 'full'
	options.url = 'http://api.qantas.com/api-docs'
}
// this is required because the generated controller is conflicting with main controller
task removeGeneratedController(type: Delete) {
    delete fileTree("${generatedCode}"+'/src/main/java/com/qantas/api') {
        include '*Controller.java'
    }
}
dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-cache')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('io.swagger:swagger-annotations:1.5.12')
    compile ( 'net.sf.ehcache:ehcache-core:2.6.5')
    compile ( 'org.apache.commons:commons-lang3:3.0')

    swaggerCodegen('io.swagger:swagger-codegen-cli:2.2.1')
	swaggerUI "org.webjars:swagger-ui:2.2.6"
	runtime('org.springframework.boot:spring-boot-devtools')
	testCompile('org.springframework.boot:spring-boot-starter-test')

}

compileJava.dependsOn tasks.generateSwaggerCode
compileJava.dependsOn tasks.generateSwaggerUI
tasks.generateSwaggerUI.dependsOn tasks.removeGeneratedController

task wrapper(type: Wrapper) {
	gradleVersion = '3.3'
}